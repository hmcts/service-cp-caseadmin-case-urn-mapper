plugins {
  id 'application'
  id 'java'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'org.springframework.boot' version '3.5.4'
  id 'org.openapi.generator' version '7.14.0'
  id 'com.diffplug.spotless' version '7.2.1'
  id 'jacoco'
  id 'maven-publish'
  id "com.github.ben-manes.versions" version "0.52.0"
  id "org.cyclonedx.bom" version "2.3.1"
}

group = 'uk.gov.hmcts.cp'
version = System.getProperty('ARTEFACT_VERSION') ?: '0.0.999'

def githubActor = project.findProperty("github.actor") ?: System.getenv("GITHUB_ACTOR")
def githubToken = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
def githubRepo = System.getenv("GITHUB_REPOSITORY")

def azureADOArtifactRepository = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
def azureADOArtifactActor = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
def azureADOArtifactToken = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")

java {
  sourceCompatibility = JavaVersion.VERSION_21
  targetCompatibility = JavaVersion.VERSION_21
}

sourceSets {
  main {
    java {
      srcDir "$buildDir/generated/src/main/java"
    }
  }
  functionalTest {
    java {
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
    resources.srcDir file('src/functionalTest/resources')
  }
  integrationTest {
    java {
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
    resources.srcDir file('src/integrationTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  // Ensure testRuntimeClasspath can be resolved for agent injection
  testRuntimeClasspath {
    canBeResolved = true
  }
}


tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.named('test') {
  useJUnitPlatform()
  systemProperty 'API_SPEC_VERSION', project.version
  failFast = true
  // Mockito must be added as an agent, see:
  // https://javadoc.io/doc/org.mockito/mockito-core/latest/org.mockito/org/mockito/Mockito.html#0.3
  jvmArgs += [
    "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
  ]
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
  reports {
    junitXml.required.set(true) // For CI tools (e.g. Jenkins, GitHub Actions)
    html.required.set(true)     // Human-readable browser report
  }
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
  useJUnitPlatform()
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
  reports {
    junitXml.required.set(true) // For CI tools (e.g. Jenkins, GitHub Actions)
    html.required.set(true)     // Human-readable browser report
  }
}
tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  useJUnitPlatform()
  failFast = true
  testLogging {
    events "passed", "skipped", "failed"
    exceptionFormat = 'full'
    showStandardStreams = true
  }
  reports {
    junitXml.required.set(true) // For CI tools (e.g. Jenkins, GitHub Actions)
    html.required.set(true)     // Human-readable browser report
  }
}

tasks.named('build') {
  dependsOn tasks.named('test')
  dependsOn tasks.named('integration')
//  dependsOn tasks.named('functional') disabling until functional tests are implemented
}

tasks.named('jacocoTestReport') {
  dependsOn tasks.named('test')
  reports {
    xml.required.set(true)
    csv.required.set(false)
    html.required.set(true)
  }
}

// check dependencies upon release ONLY
tasks.named("dependencyUpdates").configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = azureADOArtifactRepository
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifact(tasks.named('bootJar'))
      artifact(tasks.named('jar'))
    }
  }
  repositories {
    maven {
      name = "GitHubPackages"
      url = uri("https://maven.pkg.github.com/$githubRepo")
      credentials {
        username = githubActor
        password = githubToken
      }
    }
    maven {
      name = "AzureArtifacts"
      url = uri(azureADOArtifactRepository)
      credentials {
        username = azureADOArtifactActor
        password = azureADOArtifactToken
      }
    }
  }
}

//Creation of Software Bill of Materials
//https://github.com/CycloneDX/cyclonedx-gradle-plugin
cyclonedxBom {
  includeConfigs = ["runtimeClasspath"]
  skipConfigs = ["compileClasspath", "testImplementation"]
  schemaVersion = "1.6"
  componentVersion = providers.provider { project.version.toString() }
  destination = file("$buildDir/reports")
}

jar {
  enabled = true
  archiveClassifier.set('plain')
  if (file("CHANGELOG.md").exists()) {
    from('CHANGELOG.md') {
      into 'META-INF'
    }
  } else {
    println "⚠️  CHANGELOG.md not found, skipping inclusion in JAR"
  }
}

bootJar {
  archiveFileName = "${rootProject.name}-${project.version}.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

def inputSpecFile = fileTree("src/main/resources/openapi").matching {
  include("*.openapi.yml")
}.singleFile

tasks.named("openApiGenerate") {
  inputs.file(inputSpecFile)
}

//The OpenAPI generator automatically generates constructors for models based on the schema.
openApiGenerate {
  generatorName = "spring"
  inputSpec = inputSpecFile.absolutePath
  outputDir = "$buildDir/generated"
  apiPackage = "uk.gov.hmcts.cp.openapi.api"
  modelPackage = "uk.gov.hmcts.cp.openapi.model"
  generateModelTests = true
  generateApiTests = true
  cleanupOutput = true
  configOptions = [
      validateSpec                  : "true",
      dateLibrary                   : "java8",
      interfaceOnly                 : "true",
      hideGenerationTimestamp       : "true",
      useJakartaEe                  : "true",
      useBeanValidation             : "true",
      useTags                       : "true",
      useSpringBoot3                : "true",
      implicitHeaders               : "false",
      performBeanValidation         : "true",
      additionalModelTypeAnnotations: "@lombok.Builder;@lombok.AllArgsConstructor;@lombok.NoArgsConstructor",
      serializableModel             : "true",
      openApiNullable               : "false"
  ]
}

tasks.named('compileJava') {
  dependsOn tasks.named('spotlessApply')
}

def formatterUrl = 'https://raw.githubusercontent.com/hmcts/api-cp-code-style/refs/heads/main/config/formatter/eclipse-formatter.xml'
def formatterPath = "${layout.buildDirectory.get().asFile.absolutePath}/eclipse-formatter.xml"
tasks.register('downloadFormatter') {
  outputs.file(formatterPath)
  doLast {
    def file = new File(formatterPath)
    file.parentFile.mkdirs()
    file.text = new URI(formatterUrl).toURL().text
  }
}
spotless {
  java {
    target 'build/generated/src/main/**/*.java'
    removeUnusedImports()
    eclipse().configFile(formatterPath)
  }
}

tasks.named('spotlessApply') {
  dependsOn tasks.named('openApiGenerate'), tasks.named('downloadFormatter')
}
tasks.named('spotlessJava') {
  dependsOn tasks.named('openApiGenerate'), tasks.named('downloadFormatter')
}

tasks.withType(Checkstyle).configureEach {
  def generatedDir = file("${layout.buildDirectory.get().asFile.absolutePath}/generated/src/main/java").canonicalPath
  source = source.filter { file ->
    !file.canonicalPath.startsWith(generatedDir)
  }
}

application {
  mainClass = 'uk.gov.hmcts.cp.Application'
}

ext {
  log4JVersion = "2.25.1"
  logbackVersion = "1.5.18"
  lombokVersion = "1.18.38"
}

dependencies {
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
  implementation 'io.swagger.core.v3:swagger-core:2.2.34'

  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-json'

  implementation platform('io.micrometer:micrometer-tracing-bom:latest.release')
  implementation 'io.micrometer:micrometer-tracing'
  implementation 'io.micrometer:micrometer-tracing-bridge-otel'
  implementation 'io.micrometer:micrometer-registry-azure-monitor'
  implementation 'com.azure:azure-monitor-opentelemetry-autoconfigure:1.2.0'

  implementation 'net.logstash.logback:logstash-logback-encoder:8.1'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
  implementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion

  implementation('io.rest-assured:rest-assured:5.5.5') {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
  implementation 'org.hibernate.validator:hibernate-validator:9.0.1.Final'
  implementation 'org.apache.commons:commons-text:1.14.0'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testImplementation(platform('org.junit:junit-bom:5.13.4'))
  testImplementation 'org.mockito:mockito-core:5.18.0'
  testRuntimeOnly 'org.mockito:mockito-inline:5.2.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.5.4', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}
